
[Назад: Меню](../help/menu.html)

==Звук==
[:help_sound]

[:zvuk]

Quest Soft Player позволяет запускать проигрывание различных звуковых файлов.

Внимательно читайте документацию к конкретному плееру, прежде чем запускать звуковые файлы. Все плееры поддерживают ограниченные наборы форматов. Наиболее распространёнными поддерживаемыми форматами считаются `.mp3`, `.wav` и `.ogg`.

=Возможности классического плеера QSP=

	* Одновременное звучание до 32-х композиций.
	* Поддерживается множество различных аудиоформатов, в т.ч. `.mp3`, `.wav`, `.mid`, `.ogg` и другие.

=Операторы для управления звуковыми файлами=

	* `PLAY` — проигрывание указанного звукового файла с заданной громкостью. Общая запись:
		```qsp
			PLAY [$путь_к_звуковому_файлу],[#громкость]
		```
		, где `[$путь_к_звуковому_файлу]` — путь к звуковому файлу относительно файла игры, `[#громкость]` — громкость воспроизведения в процентах от 0 до 100. Параметр `[#громкость]` может отсутствовать, при этом громкость принимается равной 100%. Примеры:
		```qsp
		!Громкость 100%
		play 'sound/music.mp3'
		!Громкость 50%
		play 'sound/music.mp3',50
		!Громкость 0% (без звука)
		play 'sound/music.mp3',0
		```
		```qsp		 
		!Проигрывание файла по адресу из переменной $file
		! с расширением 'mid'
		! и громкостью volume
		$file="melody"
		play '<<$file>>.mid',volume
		! аналогично:
		$file="melody.mid"
		play $file,volume
		```
		Если файл уже проигрывается, то изменяется громкость звучания без его "перезапуска".
	* `CLOSE` — останавливает проигрывание указанного звукового файла. Общая запись:
		```qsp
		CLOSE [$путь_к_звуковому_файлу]
		```
		, где `[$путь_к_звуковому_файлу]` — путь к звуковому файлу относительно файла игры. Если параметр `[$путь_к_звуковому_файлу]` не указан, происходит остановка воспроизведения всех звуковых файлов.
		```qsp
		close "sound/music.mp3"
		```
	* `CLOSE ALL` — остановка проигрывания всех активных звуковых файлов. Имеет краткую форму `CLOSE`.
		```qsp
		! останавливаем все звуки
		close all
		! или так
		close
		```

=Функции для управления звуковыми файлами=

	* `ISPLAY` —  функция проверяет, проигрывается ли в данный момент файл с указанным названием, и если проигрывается, возвращает 1. В противном случае функция возвращает 0. Общая запись:
		```qsp
		ISPLAY([$путь_к_файлу])
		```
		, где `[$путь_к_файлу]` — путь к звуковому файлу относительно файла игры.
		Пример:
		```qsp
		if isplay('music/mountsound.mp3'):
			*pl 'Проигрывается музыка.'
		else:
			*pl 'Музыка не проигрывается.'
		end
		```

=Пути к звуковым файлам=

Пути к звуковым файлам указываются относительно файла игры.

Например, если файл игры лежит в папке "game", и в той же папке лежит звуковой файл "music.mp3", указываем просто название файла:

```qsp
play "music.mp3",100

if isplay("music.mp3"):
	"проигрывается музыка"
end
```

Если файл игры находится в папке "game", и в той же папке лежит папка "snd", а в ней находится файл "mistic.mid", то путь к этому звуковому файлу будет выглядеть, как "snd/mistic.mid":

```qsp
play "snd/mistic.mid",25
wait 1000

if isplay("snd/mistic.mid"):
	"Звук ещё не проигрался."
else:
	"Тишина"
end
```


[Вперёд: Графика](help:graphics)

