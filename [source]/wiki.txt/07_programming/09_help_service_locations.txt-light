
[Назад: Порядок работы интерпретатора](help:principle)

==Служебные локации==
[:help_service_locations]

[:sluzhebnye_lokacii]

В QSP есть возможность сделать так, что бы локация выполнялась каждый раз при возникновении определенного события. Для этого нужно присвоить название локации предназначенной для этого системной переменной. Выполнение локации происходит также, как при использовании оператора [GS](../help/organizing.html). Пример: 
```qsp

$ONOBJSEL = 'ONOBJSEL'
$ONNEWLOC = 'перерисовка'
$ONOBJADD = '' &! Убрали обработчик

```

	*  Сохранения:
		*  `$ONGLOAD` - сразу после загрузки состояния игры
		*  `$ONGSAVE` - непосредственно перед сохранением состояния игры
	*  Предметы:
		*  `$ONOBJADD` - при добавлении предмета
			*  $ARGS[0] - название добавленного предмета
			*  $ARGS[1] - путь к картинке добавленного предмета
		*  `$ONOBJDEL` - при удалении предмета
			*  $ARGS[0] - название удалённого предмета
		*  `$ONOBJSEL` - при выборе предмета
			*  Получить название выбранного предмета можно через функцию "SELOBJ". 
```qsp

IF $SELOBJ = 'чайник':
  P 'Самый обычный чугунный чайник.'
END

```

			*  При выборе играющим какого-либо предмета, он остаётся выделенным. Снять выделение можно командой UNSELECT или UNSEL. 
```qsp

IF $SELOBJ = 'нож':
  *P 'Нельзя убить этого человека'
END
UNSEL

```

	*  Локации:
		*  `$ONNEWLOC` - при переходе на новую локацию
			*  Выполняется перед передачей управления игроку (после выполнения кода всех участвующих в переходах локаций)
			*  Получить название локации, на которую был осуществлён переход, можно с помощью функции "CURLOC"
```qsp

IF $CURLOC = 'дом': кошка = 1

```

	*  Действия:
		*  `$ONACTSEL` - при выборе действия.
			*  Именно при выборе действия, а не при нажатии действия.
			*  Получить название выбранного действия можно через функцию"SELACT".
```qsp

IF INSTR(1, $SELACT, 'Пойти'): PLAY 'sounds\walk.mp3'

```

	*  Время
		*  `$COUNTER` - локация-счетчик вызывается через одинаковые промежутки времени
			*  По умолчанию промежутки 500мс, т.е. 2 раза в секунду
			*  Автоматическое обновление интерфейса срабатывает с той же частотой
			*  Промежутки задаются командой `SETTIMER` [#период] в миллисекундах 
```qsp

SETTIMER 1000/частота_в_герцах

```
```qsp

SETTIMER 1000*период_в_секундах

```

	*  Строка ввода
		*  `$USERCOM` - по нажатию "Enter" в строке ввода
			*  Подробнее по строке ввода см. [Ввод текста игроком](../help/inputs.html)
`Примечание:` При использовании операторов "KILLALL, KILLVAR" очищаются также все системные переменные.

[Вперёд: Динамический код](../help/dynamical.html)

