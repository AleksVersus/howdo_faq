действие по условию
-----------------------------------------------------------------------------
=Jack=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=138&prc=25&Itemid=57#p21604
ребята помогите решить одну проблему мне надо сделать в одном действие два действия только не знаю как правильно прописать 
пример: Act 'подать руку' 
        if frends >= 10 
        pl'мою руку радостно приняли' 
        else 
         pl 'мое предложение отвергли' 
и второе на команду обнять 1-е действие обнять нахально 
                           2-е действие не стоит этого делать

=Aleks Versus=
сть такая плюшка. "Действия по условию" называется. 

! действия начало условия 
if obnyat=0: 
    ! начало действия 
    act 'обнять': 
        ! выставляем маркер 
        obnyat=1 
        ! посещаем текущую локацию 
        goto $curloc 
    ! конец действия 
    end 
    ! начало действия 
    act 'подать руку': 
        ! начало условия 
        if friends >= 10: 
            ! если условие выполняется 
            pl 'мою руку радостно приняли' 
        else 
            ! если условие не выполняется 
            pl 'моё предложение отвергли' 
        ! конец условия 
        end 
    ! конец действия 
    end 
else 
    ! если obnyat!0 
    ! действие отмены объятий начало действия 
    act 'не стоит этого делать': 
        ! обнуляем маркер 
        obnyat=0 
        ! посещаем текущую локацию 
        goto $curloc 
    ! конец действия отмены объятий 
    end 
    ! действия обнимания: 
    act 'обнять нахально': 
        ! обнимаем нахально 
        !--- 
        !--- 
    end 
! действия конец условия 
end
-------------------------------------------------------------
=saks=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=160&prc=25&Itemid=57#p23220
задача 
выполняются множество последовательных действий, при каждом следующем действии есть выбор продолжить или уйти(будет повышаться шанс неудачи, но это к делу не относится) в итоге получается огромный код. как упростить его (допустим чтобы вариант "уйти" прописать только 1 раз а не в каждом действии) 
з.ы. логика что при прерывании действия (неудаче или "уходе") процесс должен начинаться сначала 

вот пример, при 10+ "этапах" просто стена кода 

 Спойлер Show Spoiler Hide Spoiler
  
акт"шить платье": 
cla 
*clr 
img src="pic/манекен.жпг" 
"вы начали шить платье" 
  акт"разрезать ткань": 
  cla 
  *clr 
  img src="pic/ткань.жпг" 
  "вы разрезали ткань" 
     акт"сшить лоскуты": 
     cla 
     *clr 
     img src="pic/лоскуты.жпг"
     "вы сшили лоскуты" 
     act "Уйти" : 
     cla 
     *clr 
     gs curloc 
     end 
     end 
  act "Уйти" : 
  cla 
  *clr 
  gs curloc 
  end 
  end 
act "Уйти" : 
cla 
*clr 
gs curloc 
end 
end

=evp=
act "шить платье": 
    cla 
    *clr 
    'img src="pic/манекен.жпг"' 
    "вы начали шить платье" 
    act "разрезать ткань": 
        delact $selact 
        *clr 
        'img src="pic/ткань.жпг"' 
        "вы разрезали ткань" 
        act"сшить лоскуты": 
            delact $selact 
            *clr 
            'img src="pic/лоскуты.жпг"' 
            "вы сшили лоскуты" 
        end 
    end 
    act "Уйти" : 
        cla 
        *clr 
        gs curloc 
    end 
end
Вот так можно сделать. Но у этого подхода есть один недостаток: действие "Уйти" перемещается на первую позицию в списке действий. Но это не самый главный недостаток. Гораздо важнее, что ваш код не учитывает уже совершенные действия. Например, если после действия "разрезать ткань" не выбирать "сшить лоскуты", а "Уйти", то ваш код все равно будет требовать "разрезать ткань", несмотря на то, что это действие уже сделано.

=saks=
так и задумано, что после "ухода" весь процесс надо начинать сначала 
с перемещением кнопки действительно пичаль, хотя если вынести кнопку активного действия из окна действий в основное, то может получится весьма сносно

=Aleks Versus=
http://qsp.su/index.php?option=com_agora&task=topic&id=348&Itemid=57
---------------------------------------------------------------------------------------
=Larson=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=199&prc=25&Itemid=57#p25352
Всем привет, нужна помощь! 
Например у меня в игре есть локации Комната1 (в этой комнате герой по сюжету находит ключ) и Комната2 (двери этой комнаты заперты на ключ). 
Какой код нужно написать чтобы только после посещения Комнаты1, в  Комнате2 появилось действие 'Открыть ключем' ? 
Я читал FAQ но ничего не понял. Буду очень благодарен.

=Aleks Versus=
в грубом приближении так: 
комната1 

Код:
if ключ_взят=0: 
   act 'Поднять ключ': 
      ключ_взят=1 
      addobj 'Ключ' 
      delact $selact & ! или по желанию goto $curloc 
   end 
end
комната2 

Код:
if ключ_взят=1: 
   act 'Открыть дверь ключом': 
       goto 'Комната3' 
   end 
end
---------------------------------------------------------------------------------------
=Larson=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=200&prc=25&Itemid=57#p25429
Подскажите пожалуйста, как сделать чтобы одна и та же локация по разному реагировала на присутствие определенного предмета в инвентаре. Например есть локация Комната "В комнате за столом сидит пьяный монах, он не хочет с тобой разговаривать", затем я беру в какой-то локации предмет "Бутылка вина" и когда снова прихожу в Комнату то там уже такой текст "Увидев у тебя в руках бутылку, монах словно ожил" и ниже появляется действие которого до этого небыло "Поговорить с монахом".

=Aleks Versus=
это действие по условию. Целая тема выделена: тыц http://qsp.su/index.php?option=com_agora&task=topic&id=348&Itemid=57.
---------------------------------------------------------------------------
=Larson=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=200&prc=25&Itemid=57#p25470
У меня вопрос: как сделать чтобы при наличии определенных предметов в локации появлялось действие с их использованием? Например герой бродит по подземелью, ему нужно найти предметы Алмаз, Рубин и Золото. Когда он это все находит и берет, то в определенной локации, появляется действие 'Выйти из пещеры через портал'. Но только при наличии всех троих предметов. 
Заранее спасибо.

=Aleks Versus=
если предметы добавлены в окно "инвентарь", то так: 

Код:
if obj 'Алмаз' and obj 'Золото' and obj 'Рубин': 
   act 'Выйти через портал': 
      goto 'Ойкумена' 
   end 
end
если при добавлении предметов, ты отмечал это переменными-маркерами, можно так: 

Код:
if алмаз=1 and золото>0 and рубин=1: 
   act 'Выйти через портал': 
      goto 'Ойкумена' 
   end 
end
---------------------------------------------------------------------------------------
=vitorio555=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=200&prc=25&Itemid=57#p25482
подскажите пожалуйста, как сделать так чтоб в локации кнопка перехода появлялась при определённых условиях, например, если я до этого не встречал такого то нпс, то кнопки перехода на определённую локацию не будет,а если да, то будет. И вообще мне ещё многое хочется спросить, я чайник в прогромировании, но пока хоть это узнать хотелось бы.

=Oliver=
if petro_znakom = 1: ACT 'Зайти к Василисе': GT 'vasilisa'
Насчёт времени: Календарь по методу счёта Юлианских дней 

Чтобы он появился там, нужно придумать условия, при которых он должен появится. 

Код:
! time - будет условным обозначением времени(вы свои переменные подставите) 


if time>=6 and time<= 22: 
  *pl 'За столом сидит Петро, весь измученный и побитый как старая собака. Люди шарахаются от него, как от прокаженного, но именно он вам и нужен.' 
  ACT 'Подойти к Петру': GT 'petro_bar' 
end
---------------------------------------------------------------------------------------
=waganwils=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=207&prc=25&Itemid=57#p26276
Приветствую всех разработчиков. Я недавно начал погружаться в это все дело и у меня несколько простых вопросов. 
1. Как сделать переход по локациям следующим образом: При посещение определенной локации, определенное количество раз. Скрытая локация появляется в строке локаций после выполнения условия. 
2. Каким образом сделать диалог с выбором ответов, после которых так же открываются определенные локации. (Вариант1: После диалога и выборе нужного ответа, появляется новая строка с новой локацией куда можно перейти, либо не переходить. Вариант2: После выбора ответа тебя автоматом кидает в новую локацию.)
3. Прошу совета: Как размещать множественные локации и как их называть для максимально комфортной работы с ними в дальнейшем, если их много. 
Спасибо.

=Aleks Versus=
1. Действие по условию http://qsp.su/index.php?option=com_agora&task=topic&id=348&p=1&prc=25&Itemid=57 
В поле "выполнить при посещении" локации пишешь примерно следующее. 

 Спойлер Show Spoiler Hide Spoiler
 
Код:
*p "Я нахожусь на поляне, окружённой плотной стеной кустарника. " 
здесь_был_поляна+=1 & ! увеличиваем число посещений на 1 
! если число посещений меньше 5, нет доступа к локации "болото" 
if здесь_был_поляна<5: 
   *pl "Но так и не вижу пути к болоту, о котором мне рассказывал гном." 
! в противном случае (если число посещений 5 и больше) 
else 
   *pl "И кажется я вижу тропку между двух шиповниковых кустов, которая ведёт к болоту" 
   ! начало действия 
   act "Идти к болоту": 
      goto "болото" 
   ! конец действия 
   end 
! конец условия 
end 
! действие, которое выводится всегда 
act "Вернуться в чащобу": 
   goto "лес_чащоба" 
!конец действия вернуться в чащобу 
end 
2. То же самое, что и предыдущее. Диалог реализуется в виде действий по условию, просто в некоторых действиях вставляешь команды перехода. Есть примеры диалогов. http://qsp.su/index.php?option=com_content&view=article&id=72:-11&catid=36:2009-02-19-06-11-21&Itemid=76
3. Во-первых, в QuestGenerator есть папки. Это очень облегчает написание кода. 
Во-вторых, можно разбить игру на модули/части. Допустим у меня разбито: вводные данные, движок (служебные локации), библиотеки (оружие, броня, предметы и прочее), сама игра. Потом уже на самой первой локации в игре подключаешь всё это с помощью команды addqst (inclib для Quest Navigator), типа: 
Код:
addqst 'drive.qsp' 
addqst 'base.qsp' 
addqst 'intro.qsp' 
естественно, приходится работать в нескольких окнах. 
Ну и в-третьих, я как-то со временем взял себе за правило локациям с разным назначением задавать разные шаблоны имён. Например, служебные локации состоят у меня из двух-трёх (четырёх, пяти, но это реже) слов, разделённых точкой get.daughter.obj. Локации мест имеют вид место;название_локации. Локации сюжетные (т.е. где воспроизводятся большие отбивки сюжета) как-то так: 0.1_пролог  0.2_пролог  1.1_сцена_в_баре  1.2_сцена_в_баре  1.3.1_поражение_в_баре  1.3.2_победа_в_баре. Локации различных инфо-листов и прочие информационные имеют в начале и конце названия квадратные скобки: [start] [info] [thanks] [credits] [end] [death].
----------------------------------------------------------------------------
=Kaspar=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=214&prc=25&Itemid=57#p26716
Ребят, такой вопрос, пилю что - то типа визуальной новеллы, и хотелось бы проконсультироваться по аспектам кода. 
Допустим на локации в описании посередине портрет персонажа, под портретом его имя выделенное жирным, и минуя одну пустую строчку то, что персонаж говорит. 
Так вот, как возможно реализовать вывод текста по клику кнопки далее в действиях? Как бы, я пытался сделать это через act, но штрудирование руководств мне не помогло и текст просто выводился лишь по первому нажатию кнопки далее, затем кнопка пропадала, хотя дальнейший акт с тем же названием, но другим текстом уже имеется. 
Надеюсь на вашу помощь, заранее спасибо)

=Oliver=
if dialog = 0: 
    'Текст 1' 
    act 'Далее': dialog = 1 & gt curloc 
end 
if dialog = 1: 
    'Текст 2' 
    act 'Далее 2': dialog = 0 & gt curloc 
end

=Kaspar=
Спасибо, а переменной "диалог" нужно задавать значение заранее?

=oleksandrigo=
необязательно. Если ты ее не используешь то она 0 равна.
-----------------------------------------------------------------
=новичок=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=219&prc=25&Itemid=57#p27064
как сделать, что бы при покупке предмета, он добавлялся в определенную локацию

=Aleks Versus=
если я правильно тебя понял: 

Код:
# магазин
if подушка_куплена=0:
   act "Купить подушку и отправить домой":
      подушка_куплена=1
      goto $curloc
   end
end
--- магазин ---------------------------------

# спальня
if подушка_куплена=1 and подушка_взята<>1:
   *pl "На кровати лежит подушка" 
   act "Взять подушку": 
       addobj "Подушка" 
       подушка_взята=1 
       goto $curloc 
   end 
end 
--- спальня ---------------------

----------------------------------------------------------------
=Katy=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=139&prc=25&Itemid=57#p21642
А как сделать так, чтобы после того, как действие сработало один раз и исчезло, появилось новое действие? 
У меня действие исчезает, а новое, которое должно было появится вместо старого, не появляется.

=Aleks Versus=
это можно сделать несколькими способами. Самый простой - вложить действие в действие: 

act 'Поднять хомяка': 
! начало действия поднять хомяка 
    ! выводим сопроводительный текст в основное описание 
    *pl 'Вы взяли хомяка на руки, но он стал отчаянно вырываться.' 
    ! удаляем текущее действие 
    delact $selact 
    ! вставляем новое действие 
    act 'Отпустить хомяка': 
    ! начало действия отпустить хомяка 
        ! выводим сопроводительный текст в основное описание 
        *pl 'Вы отпустили хомяка, и он тут же исчез в кустах.' 
        ! удаляем текущее действие 
        delact $selact 
    ! конец действия отпустить хомяка 
    end 
! конец действия поднять хомяка 
end
Есть другой способ действие по условию. Пишешь два разных действия, но одно выводится при одном значении переменной, другое при другом: 
 
Код:
! начало многострочной конструкции условия: 
if хомяк = 0: 
! если хомяка в руках нет 
    act 'Поднять хомяка': 
    ! начало действия поднять хомяка 
        ! выводим сопроводительный текст в основное описание 
        *pl 'Вы взяли хомяка на руки, но он стал отчаянно вырываться.' 
        ! выставляем маркер, чтобы знать, что хомяк в руках 
        хомяк = 1 
        ! возвращаемся на текущую локацию 
        goto $curloc 
    ! конец действия поднять хомяка 
    end 
elseif хомяк = 1: 
! в противном случае, если хомяк в руках 
    act 'Отпустить хомяка': 
    ! начало действия отпустить хомяка 
        ! выводим сопроводительный текст в основное описание 
        *pl 'Вы отпустили хомяка, и он тут же исчез в кустах.' 
        ! выставляем маркер, чтобы знать, что хомяк отпущен 
        хомяк = 2 
        ! возвращаемся на текущую локацию 
        goto $curloc 
    ! конец действия отпустить хомяка 
    end 
elseif хомяк = 2: 
! в противном случае, если хомяк убежал 
    ! ничего не делаем, этот пункт можно исключить 
! конец многострочной конструкции условия 
end