динамическое меню
--------------------------------------------------------------------
=Lisichka=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=114&prc=25&Itemid=57#p19982
Помогите, пожалуйста. 
По ходу игры игрок взаимодействует только с одним предметом - венком. Постепенно я в него с помощью массивов добавляю цветы. Но, когда я сразу после добавления цветка жму на венок, цветок там не появляется, только когда жму второй раз. Никак нельзя это исправить? Вот пример кода: 

if $selobj = 'Венок': 
	$fl[0] = 'Шиповник:!цветы_шиповник:content/шиповник.jpg' 
	$fl[1] = 'Дуб:!цветы_дуб:content/дуб.jpg' 
	menu '$fl' 
	unselect 
	if фиалка = 1: 
		$fl[2] = 'Фиалка:!цветы_фиалка:content/фиалка.jpg' 
		menu '$fl' 
		unselect 
	end 
	if солома = 1: 
		$fl[3] = 'Солома:!цветы_солома:content/солома.jpg' 
		menu '$fl' 
		unselect 
	end 
end

=Storm=

$fl[0] = 'Шиповник:!цветы_шиповник:content/шиповник.jpg' 
$fl[1] = 'Дуб:!цветы_дуб:content/дуб.jpg'

Я так понимаю, переменные $fl[0] и $fl[1] заданы уже сразу, поэтому их можно прописать где-нибудь в стартовой локации, с которой начинается игра, или там где меню игры. 
А этот код прописать в локации, которая отвечает за выбор предмета: 

if фиалка=1: 
$fl[2] = 'Фиалка:!цветы_фиалка:content/фиалка.jpg' 
end 
if солома = 1: 
$fl[3] = 'Солома:!цветы_солома:content/солома.jpg' 
end 
if $selobj = 'Венок': menu '$fl' 
unselect

UPD: Можно ещё проще без переменных "фиалка" и "солома". Просто задавать элемент массива тогда, когда добавляется новый цветок. Код станет ещё проще.

=Ajenta=
А можно файл игры на почту? потому что так непонятно что происходит. 

И я бы не советовала индексы ставить, лучше так:

$fl[] = 'Солома:!цветы_солома:content/солома.jpg' 

кусп сам расставит, по очереди, а Вы не будете путаться каким по счёту добавился цветок.

=Storm=

Это тот вариант без переменных "фиалка" и "солома", выполненный на двух локациях.

!Стартовая локация 
KILLVAR 
$ONOBJSEL='Предметы' 
addobj "Венок" 
ACT'Добавить фиалку': 
DELACT 'Добавить фиалку' 
$fl[2] = 'Фиалка:!цветы_фиалка:content/фиалка.jpg' 
end 
ACT'Добавить солому': 
DELACT 'Добавить солому' 
$fl[3] = 'Солома:!цветы_солома:content/солома.jpg' 
end 
$fl[0] = 'Шиповник:!цветы_шиповник:content/шиповник.jpg' 
$fl[1] = 'Дуб:!цветы_дуб:content/дуб.jpg'

!Локация "Предметы" в переменной $ONOBJSEL 
if $selobj = 'Венок': menu '$fl' 
unselect

Немного теории: код воспроизводиться программой по порядку в котором ты его пишешь. В твоём первоначальном коде, когда мы вызывали локацию-обработчик выбора предмета мы вначале вызывали меню, а потом уже присваивали значения элементам массива "fl". Поэтому меню у нас выводилось не полным при первом нажатии, так как при первом нажатии программе ещё не известны элементы массива $fl[2] и $fl[3].

=Lisichka=
урраааа, я переставила его вниз, и все заработалооо! Спасибо, не знала, что нужно писать в определенном порядке!

=Ajenta=
А ещё лучше даже проще заменить код на обжсел на такой 

if $selobj = ' ': 
	$fl[0] = 'Шиповник:!цветы_шиповник:content/шиповник.jpg' 
	$fl[1] = 'Дуб:!цветы_дуб:content/дуб.jpg' 
	if фиалка=1 and добавили_фиалку = 0: 
		добавили_фиалку = 1 
		$fl[] = 'Фиалка:!цветы_фиалка:content/фиалка.jpg' 
	end 
	if солома = 1 and добавили_солому = 0: 
		добавили_солому = 1 
		$fl[] = 'Солома:!цветы_солома:content/солома.jpg' 
	end 
	menu '$fl' 
end 

unsel

И другие цветы по тому же принципу добавлять, как здесь. В таком варианте не важен порядок добавления.

=Aleks Versus=
Вообще код Лисички правится довольно просто. На ошибку указал Storm: Лисичка вызывает меню до того, как сформируются новые пункты меню. Дельное замечание дала и Ajenta, так как если бы игрок взял солому раньше фиалки, то второй пункт меню так и не был бы создан.

Таким образом код можно поправить так:

if $selobj = 'Венок':
	killvar '$fl'
	$fl[] = 'Шиповник:!цветы_шиповник:content/шиповник.jpg'
	$fl[] = 'Дуб:!цветы_дуб:content/дуб.jpg' 
	if фиалка = 1: 
		$fl[] = 'Фиалка:!цветы_фиалка:content/фиалка.jpg'
	end 
	if солома = 1: 
		$fl[] = 'Солома:!цветы_солома:content/солома.jpg'
	end
	menu '$fl' 
end
!...
unselect

Последний код, который предложила Ajenta не совсем ясен, но приблизительно я понимаю, что она хотела сделать.
Новые пункты меню добавляются только один раз, таким образом меню формируется по ходу набора цветов, сохраняя порядок, в котором игрок поднимает цветы. Перепишем его так, чтобы он однозначно работал:

if $selobj = 'Венок':
	if arrsize('$fl')=0:
		$fl[] = 'Шиповник:!цветы_шиповник:content/шиповник.jpg' 
		$fl[] = 'Дуб:!цветы_дуб:content/дуб.jpg'
	end
	if фиалка=1 and добавили_фиалку = 0: 
		добавили_фиалку = 1 
		$fl[] = 'Фиалка:!цветы_фиалка:content/фиалка.jpg' 
	end 
	if солома = 1 and добавили_солому = 0: 
		добавили_солому = 1 
		$fl[] = 'Солома:!цветы_солома:content/солома.jpg' 
	end 
	menu '$fl' 
end 

unsel