отложенный переход/действие
-------------------------------------------------------------------------------------------
=BraJelon=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=127&prc=25&Itemid=57#p20887
Такой вопрос. К примеру у меня есть две локации. Как сделать так, чтобы при посещении локации "1" спустя секунду меня перекидывало на локацию "2" автоматически.

=Lisichka=
Используйте оператор wait, время указывается через пробел в миличекундах 
Более подробно об этом можно прочитать в вики) 
Например: 

!локация '1' 
'Здесь текст описания.' 
wait 2000 
gt '2'

=BraJelon=
Спасибо за ответ. Оказывается я делал все правильно, просто в следующей локации отсутствовал фон и при смене локации, картинка оставалась, вот я и думал, что локация не сменилась.

=Aleks Versus=
Вообще, это не совсем правильно, поскольку оператор wait подвешивает игру на определённое время. Необходимо очень аккуратно использовать этот оператор. Лучше воспользоваться возможностями локации counter и функции msecscount.
---------------------------------------------------------------------------
=Ontario=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=148&prc=25&Itemid=57#p22355
Вопрос простой, но помощь крайне нужна. 
Как сделать, чтобы действие открывалось спустя некоторое время проведенное на локации. Я пробовал через wait, но это останавливает игру полностью. Мне же нужно, чтобы игрок мог выбирать другие варианты и уйти с локации, или получить новый вариант в список если он не будет торопиться. 
Заранее спасибо.

=Aleks Versus=
тебе тот же совет, что и Svartbergу: воспользуйся локацией-счётчиком $counter. В моём сообщении от 19.Дек.15 12:35:51 (выше) представлен примерный способ отсчёта времени. Просто тебе нужно будет посчитать время проведённое на локации. 

Вот рабочий пример actattime.qsp http://qsp.su/components/com_agora/img/members/248/actattime.qsp Подсчёт времени можно исключить в принципе. Достаточно части с вычислением текущих значений.

------------------------------------------------------------------------------
=vlad=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=182&prc=25&Itemid=57#p24390
Здравствуйте. Недавно начал изучение QSP, появилось несколько трудностей: 
1. Как зациклить проигрывание аудио? Желательно с примером. (Знаю что здесь вопрос уже поднимался но разобраться не удалось). 
2. Как сделать переход между тремя локациями A B C по схеме: локация А - кнопка перехода на локацию В - на локации В ожидание несколько секунд и автоматический переход на локацию С. 

=Aleks Versus=
зациклить аудио: 

Код:
!самая первая локация в игре 
$counter = 'counter' 
!создаёшь локацию counter и пишешь в ней 
if isplay('путь к мелодии/мелодия.mp3')=0: 
   play 'путь к мелодии/мелодия.mp3' 
end
Того, что ты описал во втором вопросе, лучше не делать. Вернее: так делать плохо. Но делается это так. В локации B в поле выполнить при посещении: 

Код:
wait 5000 & ! задержка пять секунд 
goto 'C'
--------------------------------------------------------------------------------------
=Aaaspavel=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=188&prc=25&Itemid=57#p24722
Как сделать что-то вроде быстрого теста, выдается текст с вопросом, потом на несколько секунд появляются варианты ответов и если не успел нажать на правильный ответ или вообще нажать, то задается следующий вопрос... что то навроде действия по таймеру, только я не совсем понял как это оформить, чтобы 
текст вопроса 
через 30 сек появляются ответы 
act ответ1 
act ответ2 
act ответ3 
через 5 сек переход к другому вопросу 
текст вопроса 
через 38 сек появляются ответы и так далее

=Aleks Versus=
локация counter: 

 Спойлер Show Spoiler Hide Spoiler
 
Код:
! и т.д. 
if marker=5 and (time + 30000)<=msecscount: 
    time=msecscount 
    act 'первый ответ': 
        point +=5 
        clr 
        pl point 
        *clr 
        cla 
        marker=7 
    end 
    act 'второй ответ': 
        point +=10 
        clr 
        pl point 
        *clr 
        cla 
        marker=7 
    end 
    act 'третий ответ': 
        point +=20 
        clr 
        pl point 
        *clr 
        cla 
        marker=7 
    end 
    marker=6 
end 
if marker=4 or (marker=3 and time+5000 <=msecscount): 
    *clr 
    cla 
    'Вопрос номер 2. Сейчас появятся варианты ответов, и у вас будет только пять секунд, чтобы выбрать один из них.' 
    marker=5 
end 
if marker=2 and (time + 30000)<=msecscount: 
    time=msecscount 
    act 'первый ответ': 
        point +=5 
        clr 
        pl point 
        *clr 
        cla 
        marker=4 
    end 
    act 'второй ответ': 
        point +=10 
        clr 
        pl point 
        *clr 
        cla 
        marker=4 
    end 
    act 'третий ответ': 
        point +=20 
        clr 
        pl point 
        *clr 
        cla 
        marker=4 
    end 
    marker=3 
end 
if marker=1: 
    'Вопрос номер 1. Сейчас появятся варианты ответов, и у вас будет только пять секунд, чтобы выбрать один из них.' 
    marker=2 
end 
if marker = 0: 
    time = msecscount 
    marker=1 
end 
clr 
pl point


=Aaaspavel=
ничё не понял, вы предлагаете мне всё сделать в одной единственной локации?

=Aleks Versus=
я предлагаю воспользоваться локацией-счётчиком. Во скольки единственных локациях будешь делать, это уже тебе решать. Но вот рабочий пример. Как я понял, ты не мог представить, как это реализуется. Я показал наглядно. Допили до варианта, который тебе нужен. 

События, привязанные к действительному времени, можно контролировать только через локацию-счётчик. По-крайней мере активные действия ты больше никак не сделаешь. wait лишит возможности выбирать вариант в течении 5 секунд. 

http://wiki.qsp.su/help:realtime
http://wiki.qsp.su/help:service_locations
---------------------------------------------------------------------------------------
=Larson=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=199&prc=25&Itemid=57#p25399
У меня есть 2 вопроса по звукам в QSP: 
1) Например у меня есть локация Порог дома, с таким кодом: 

act 'Постучать в дверь': 
delact $selact 
'Тебе никто не открыл!' 
end 

Можно ли както вставить в код звук, чтобы в игре при выборе действия 'Постучать в дверь' воспроизводился звук? 

2) Какой нужен код чтобы при переходе на локацию, звук в ней воспроизводился не сразу а например через 5 сек.? 

Буду очень благодарен за помощь!

=Aleks Versus=
команды для работы со звуком — здесь http://wiki.qsp.su/help:sound. 
В сам код звук не вставляется. Сохраняешь звук отдельным файлом, рядом с игрой или во вложенной папке, потом для действия: 

Код:
act 'Постучать в дверь': 
   'Тебе никто не открыл!' 
   play 'путь к файлу/knockknock.mp3' 
   delact $selact 
end
По второму вопросу: 
На самой первой локации в игре пишешь: 

Код:
$counter='counter'
создаёшь локацию с именем counter, на ней такой код: 

Код:
! если текущая локация та, на которой нам надо воспроизвести звук 
if $curloc='старое_кладбище': 
    ! фиксируем время в милисекундах 
    if timer=0: timer=msecscount 
    ! сравниваем текущее время и зафиксированное 
    if msecscount>timer+5000 and звук_проигран=0: 
    ! если текущее время больше чем фиксированное + 5 сек 
    ! и звук не воспроизводился 
        ! воспроизводим звук 
        play 'sound/deadwalkvoice.wav' 
        звук_проигран=1 
    end 
end