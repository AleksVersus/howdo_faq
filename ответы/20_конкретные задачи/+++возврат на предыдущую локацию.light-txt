возврат на предыдущую локацию
---------------------------------------------------------------------------------------
=Драккан=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=123&prc=25&Itemid=57#p20673
Задам еще 2 вопроса, как после случайного события вернуться на локацию срабатывания этого события. 
IF событие > 90 AND событие <= 100: 
            !На вас напала крыса 
            gt 'Бой' 
Происходит бой, как после победы вернуться на ту локацию в которой случилось событие? 

=Axil=
Вопрос 1: Попробуй запомнить имя локации на которую нужно вернуться в какой-нибудь переменной. 

=evp=
http://qsp.su/index.php?option=com_agora&task=topic&id=751&Itemid=57
--------------------------------------------------------------------------------------
=svandrus=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=142&prc=25&Itemid=57#p21893
2. При переходах с локации на локацию приходится прибегать к этому: 

Код:
$retloc = $CURLOC
чтобы без проблем вернуться куда надо. Однако, если локация с помощью ARGS поделена на секторы, то фокус не срабатывает и вернуться в нужный сектор не получается. 
Делать приходится так: 

Код:
loc #world 
IF $ARGS[0] = 'forest': 
$retloc = $CURLOC 
$place = 'forest' 
... 
END 

loc #house 
... 
GT $retloc, $place
Вопрос: можно ли в одну переменную $retloc сразу сложить все передаваемые аргументы? 

Код:
$retloc = $CURLOC, 'forest'
Вот это неправильно. Потому что не работает.

=evp=
Как-то так? 

Код:

loc #dynamics 
$goBack = { 
    gt $retloc, $ARGS[0] 
} 

loc #world 
IF $ARGS[0] = 'forest': 
$retloc = $CURLOC 
... 
END 

loc #house 
... 
dynamic $goBack, 'forest'
-----------------------------------------------------------
=Сергей=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=189&prc=25&Itemid=57#p24764
Как организовать мобильный телефон с функций, собственно, телефона и браузера (без возможности выбросить или выложить). По сути нужна переносимая локация в окне предметов .

=St-john=
Уважаемый Сергей, вот то, что вам необходимо: 
http://wiki.qsp.su/help:objs 
http://wiki.qsp.su/help:menu 
Разделы про объекты и добавление меню. Там нет ничего сложного. Все в ваших руках, тем более никто лучше вас не знает, что Вы подразумеваете под, собственно, функцией браузера. Телефон должен из игры выходить в интернет? 
Телефонные контакты можно организовать в виде гиперссылок. Что-то вроде: 

Код:
<a href="exec:!код, который нужно выполнить при нажатии на ссылку">текст, который будет выведен на экран</a>
для каждого контакта.

=Сергей=
Везде описывается, именно, предмет со всеми вытекающими... Как к предмету можно применить выход на другую локацию с последующим возвращением в ту локацию, где этот предмет был применён ?

=St-john=
Сразу видно, что Вы посчитали лишним, видимо, почитать сначала документацию, и уже потом выяснять здесь то, что недопоняли. 
Если я сам все правильно понял, то вам сюда: 
http://wiki.qsp.su/help:locations 
А если конкретнее - то вам необходимо понять разницу между goto и gosub. В менюшках как раз и используется gosub переход. Если совсем уж упрощать, то goto - это непосредственно переход на локацию, а gosub - считывание информации с какой-либо локации и возврат обратно. Учтите, что это не совсем корректное описание, но возможно, так понятнее будет. 
Поэтому при использовании объектов и меню Вы будете оставаться в той же локации, где и применяете объект.

=Сергей=
Вот я сделал телефон с контекстным меню: "Позвонить", "Браузер". Нажимаю "Позвонить"- перехожу в локацию "Контакты" в основном окне с активными ссылками, нажимаю на ссылку- перехожу в диалоги, поговорил- возвращаюсь в "Контакты", И как теперь отсюда выйти назад в локацию, в которой я нажал "Позвонить"? Что надо написать в локации "Контакты"?

=St-john=
Вот, это уже более конкретный разговор. Не вижу кода, но скорее всего достаточно добавить в локацию 'Контакты' следующее: 

Код:
act 'Закрыть телефон': 
	goto $curloc 
end
---------------------------------------------------------------------------------------
=NaViGaToR=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=198&prc=25&Itemid=57#p25296
Хочу сделать кнопку "Назад" которая возвращала на последнюю посещенную локацию. Подскажите код.

=Aleks Versus=
создаём локацию, которая будет отвечать за выполнение кода, когда посещаешь другую локацию. Название может быть любым, например зашёлНаЛокацию. И пишем на ней следующее: 

Код:
!зашёлНаЛокацию 

! возвращаем предыдущую посещённую: 
$back = $now 
! запоминаем теперешнюю, 
! чтобы в следующий раз вернуть её, 
! как предыдущую: 
$now = $curloc
Теперь нужно дать понять игре, что именно код на этой локации надо выполнять всякий раз, когда посещаешь другие, для этого в самом начале игры пишем: 

Код:
$onnewloc = 'зашёлНаЛокацию'
Ну и остаётся только создать действие в нужном месте: 

Код:
act 'Назад': 
   goto $back 
end
Если хочешь сделать возможность пролистывать локации в обратном порядке как страницы: 

 Спойлер Show Spoiler Hide Spoiler
 
Код:
! зашёлНаЛокацию 
if $args[0]='': 
! если мы посещаем новую локацию 
    ! записываем последнюю посещённую локацию 
    ! новую ячейку массива 
    $back[]=$curloc 
elseif $args[0]='back': 
! если мы возвращаемся на предыдущую 
    ! удаляем последнюю ячейку массива 
    killvar '$back',arrsize('$back')-1 
end
Действие возврата теперь выглядит так: 

Код:
act 'Назад': 
    goto $back[arrsize('$back')-2],'back' 
end
-----------------------------------------------------------------------
=Amadeus=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=212&prc=25&Itemid=57#p26617
Возможно, вопрос прозвучит максимально глупо, но... 
Есть ли в QSP команда, которая отвечает за возврат на предыдущую локацию? Пример: 
Есть локация, в которую можно перейти с нескольких других локаций. И нужно, чтобы каждый раз при нажатии на кнопку "возврат" возвращало не в определенную локацию, а на предыдущую.

=Aleks Versus=
определённой команды нет. Но делается это весьма просто. 
На самой первой локации в игре задаём локацию-обработчик события посещение новой локации: 

Код:
$onnewloc = "onNewLoc"
Затем создаём локацию с именем onNewLoc и на ней пишем: 

Код:
! эта строка прописывает в переменную $back_loc имя предыдущей посещённой локации: 
$back_loc=$current_loc 
! эта строка прописывает в переменную $current_loc имя текущей локации: 
$current_loc=$curloc 
! теперь воссоздаём возврат в виде действия, например: 
act "Вернуться": 
     goto $back_loc 
end
Если нужно действие, которое может провести по цепочке локаций в обратную сторону, сделать это можно, написав на локации onNewLoc следующий код: 

 Спойлер Show Spoiler Hide Spoiler
 
Код:
if $back[]<>$curloc and back_act=0: 
! если в последнем элементе массива нет названия текущей локации 
! а так же если мы совершаем переход не по действию возврата 
    !записываем в последний элемент массива название текущей локации 
    $back[]=$curloc 
end 
! обнуляем переменную back_act, чтобы разрешить запись в массив $back, если мы захотим перейти на новую локацию 
back_act=0 
! создаём действие возврата 
! только если есть куда возвращаться 
if arrsize('$back')>1: 
    act "шаг назад": 
        ! выставляем переменной-маркеру back_act значение 1, 
        ! чтобы запретить запись в массив $back при возврате на предыдущую локацию 
        back_act=1 
        ! непосредственно переход 
        ! записываем во временную переменную название предыдущей посещённой локации 
        $args[0]=$back[arrsize('$back')-2] 
        ! удаляем из массива текущую локацию 
        killvar '$back',arrsize('$back')-1 
        ! переходим 
        goto $args[0] 
    end 
end
http://qsp.su/components/com_agora/img/members/248/example.qsp
-------------------------------------------------------------------
=WastedCarl=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=213&prc=25&Itemid=57#p26646
Еще один; 
Инветарь продуман как отдельная локация 
Как сделать выход с нее равносильным ? 
То есть вместо того чтобы act "Назад" goto / переносил на / мы сделаем так чтобы благодаря act 'Назад 'мы можем переходить и на локацию /2 и на остальные,в зависимости от того откуда мы переходим на локацию 'Инвентарь' благодаря предмету 'Инвентарь' 

Заранее,спасибо

=oleksandrigo=
Так. Я не очень понял вопроса. Что значит равносильным? В чем проблема act 'назад'? Или тебе надо чтобы с инвентаря можно было в другой город попасть? Или что? 
Upd Ааа или тебе надо чтобы оно сохраняло какая была пред локация и в зависимости от этого были возможности прыгать в другие локации?? 
Если upd был правильным то что-то вроде... 
При переходе в инвентарь нужно сделать не просто gt 'инвентарь', а добавить к нему аргумент.

Код:
gt 'инвентарь', $curloc
То бишь мы в аргументе передаем название локации из которой мы попали в инвентарь. А потом уже в инвентаре 

Код:

if args[0] = 'Таверна': act 'Перейти кудато': gt "Кудато" 

=Svartberg=
Бога ради, не пользуйся дефолтным инвентарем. Забудь про него, больше проблем и мороки с ним. 
Если предметов много, то заделай из них массив с простейшими идентификаторами, да сделай цикличную выгрузку инфы из массивов, для выведения инвентаря. 
Дальше уж объяснять не буду, вроде понятно. Если что, поковыряйся в моих старых проектах. Там хоть все в довольно примитивном виде, но работает как часы даже с большой нагрузкой.

http://qsp.su/components/com_agora/img/members/1570/FILECODE.zip