статы
--------------------------------------------------------------------------
=BlackFox=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=144&prc=25&Itemid=57#p22092
В каком разделе FAQ можно найти инфу о создании статов персонажа и изменение при взаимодействии  
К примеру Голод:50 (варьируется от 0 до 100) 
Действие покушать +45 к стату голода 
и к примеру за 1 час игрового времени статус голод снижается на 5. 

Боже сейчас нагружу вопросамиsad извините 
Как поставить время и дни в игре и затраты времени на действия например спать, кушать 
дней надо как обычно 7, время 24 
А бы взял из примера, но там много дополнительных деталей с которыми я сейчас не справлюсь 

Ещё раз извиняюсь и благодарю за помощь 
Наверное надо было начать с игры по проще

=Aleks Versus=
"статы" - понятие очень размытое. Задавай вопрос яснее, и тогда ответ не заставит себя ждать. 
Итак. Тебе нужно добиться чего? Чтобы всё время на протяжении игры где-то отображалась информация о голоде, здоровье и прочем? 
Сначала необходимо завести переменные, которые всё это будут подсчитывать (пример): 

Код:
сытость = 100 
максимум_сытость = 100 
здоровье = 100 
максимум_здоровье = 100
Теперь, предположим, что ты хочешь, чтобы информация по сытости и здоровью выводилась в окно дополнительного описания всякий раз при переходе на новую локацию. Для этого создаём локацию-обработчик перехода на новую локацию. Название может быть любым, но обычно берут onNewLoc чтоб не путаться. Создали локацию onNewLoc. Теперь на самой первой локации в игре пишем: 

Код:
$onnewloc = 'onNewLoc'
Это даст плееру понять, какую именно локацию использовать как обработчик перехода на новую локацию. Написали. 
Теперь пишем на локации onNewLoc следующий код: 

Код:
clr & ! очистка окна дополнительного описания 
*pl "Здоровье: <<здоровье>>|<<максимум_здоровье>>" 
*pl "Сытость: <<сытость>>|<<максимум_сытость>>"
Готово. Однако при любом изменении здоровья или сытости последний кусочек кода придётся дублировать в том месте, где произошло изменение. Например: 

Код:
act 'Съесть шоколадку': 
   здоровье = здоровье + 5 
   сытость = сытость + 35 
   clr & ! очистка окна дополнительного описания 
   *pl "Здоровье: <<здоровье>>|<<максимум_здоровье>>" 
   *pl "Сытость: <<сытость>>|<<максимум_сытость>>" 
end
Чтобы всякий раз не переписывать этот длинный кусочек, рекомендую вынести его на отдельную локацию и обозвать её как-нибудь "вывод_статов". Тогда можно будет писать только одну строчку: 

Код:
act 'Съесть шоколадку': 
   здоровье = здоровье + 5 
   сытость = сытость + 35 
   gosub 'вывод_статов' 
end
Чтобы какой-то параметр не мог превысить определённое значение, используются конструкции условий. Например, применительно к нашему действию: 

Код:
act 'Съесть шоколадку': 
   здоровье = здоровье + 5 
   сытость = сытость + 35 
   if (здоровье > максимум_здоровье): здоровье = максимум_здоровье 
   ! если здоровье больше максимально возможного значения, приравниваем его значение к максимальному 
   ! и для сытости 
   if (сытость > максимум_сытость): сытость = максимум_сытость 
   gosub 'вывод_статов' 
end
------------------------------------------------------------------
=Oliver=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=151&prc=25&Itemid=57#p22510
Народ, вопрос. 
Есть здоровье, и есть мана. Как сделать так, чтобы когда здоровье на максимуме, мана не использовалась при лечении, и не лечила не чего. Желательно в виде кода.

=DaGrozz=
if health<healthMax: 
     if mana>=healthMax-health: 
     mana-=healthMax-health 
     health=healthMax 
     else 
     health+=mana 
     mana=0 
     end 
else 
health=healthMax 
end 

=evp=
B.1

Код:
health_max = 100 
if health < 0: health = 0 
if mana < 0: mana = 0 
if health <= health_max and mana > 0: 
    act "Лечиться": 
        if mana >= health_max - health: 
            mana -= health_max - health 
            health = health_max 
        else 
            mana = 0 
            health += mana 
        end 
    end 
end
B.2 

Код:
health_max = 100 
if health < 0: health = 0 
if mana < 0: mana = 0 
act "Лечиться": 
    if health < health_max: 
        if mana >= health_max - health: 
            mana -= health_max - health 
            health = health_max 
        else 
            mana = 0 
            health += mana 
        end 
    end 
end

=Svartberg=
Да тут на самом деле все очень просто) 
Выведи переменные в 'start' локацию: 

Код:

hp.max = 100 & hp.all = hp.max 
mp.max = 100 & mp.all = mp.max 
action.heals = 0 & !Переменная активирующая востановление здоровья 
heals.mp = 25 & !Переменная определяющая кол-во необходимой маны для востановления здоровья. 
heals.hp = 25 & !Переменная определяющая кол-во востанавлиаемого здоровья 
Далее выведем отдельную локацию, которая будет следить за параметрами жизней и маны: 

Код:

IF hp.all < hp.max: 
   IF action.heals = -1 and mp.all >= heals.mp: 
      hp.all += heals.hp 
      mp.all -= heals.mp 
      action.heals = 0 
   ELSEIF action.heals = -1 and mp.all < heals.mp: 
      !Тут можно вывести инфу о нехватке маны) 
   END 
ELSEIF hp.all >= hp.max: 
   hp.all = hp.max 
   IF action.heals = -1: 
    !нечего не произойдет, оставил пустым если приспичит вывести информацию о невозможности применения способности. 
    action.heals = 0 
   END 
ELSEIF hp.all <= 0: 
   hp.all = 0 
END 
IF mp.all >= mp.max: 
   mp.all = mp.max 
ELSEIF mp.all <= 0: 
   mp.all = 0 
END 
-------------------------------------------------------------------------------------------
=skunsss=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=171&prc=25&Itemid=57#p23757
Привет всем.Я впервые начал делать игру на qsp.До этого с программированием  имел дело лет 20 назад- в школе. 
Как прикрутить потребности(голод ,сон) / статистику в игру? Календарь(юлианский) и время я сумел сделать...А вот с потребностями в  у меня полный стопор получился.3дня мучаюсь - и ничего не получается.Извините, что спрашиваю, наверное ,такой банальный вопрос  cool

=Aleks Versus=
Что, позволь спросить, ты имеешь ввиду? Любой параметр в игре можно описать переменной: 

Код:
голод = 0 
сон = 20 
!и т.д.
Тебе нужно, чтобы параметр изменялся с определёнными событиями? Тогда с какими событиями должно быть связано изменение? Или оно происходит с течением времени? Или ты не знаешь, где и как выводить на экран информацию при изменении параметра? Слово "прикрутить" - очень расплывчатое.

=Aleks Versus=
Например ,что-б, гг хотелось есть через определенное время.Скажем- спать, например через 18 часов, а есть, каждые- 6 часов.

очень просто. Там же, где у тебя меняется параметр времени, меняешь значение характеристики. 

Временная система отсюда : 
http://qsp.su/index.php?option=com_agor … ;Itemid=57
отсюда-то отсюда, но ты понимаешь, как она работает? )) 

Текущее время хранится в переменной time. 
Локация calendar - пересчитывает значения. 
Когда в игре происходит какое-то действие, должно изменяться текущее время и должны быть пересчитаны значения: 

Код:
act 'Ехать на вокзал': 
        time += 45 & ! изменяем текущее время (прибавили сорок пять потраченных минут) 
        gs 'calendar'  & ! пересчитываем значения 
        gt 'station'  & ! отправляемся на локацию вокзала 
end
Вот здесь как раз при изменении времени можно было изменить и значения бодрости и голода. Всё верно: 

Код:
act 'Ехать на вокзал': 
       time += 45 & ! изменяем текущее время (прибавили сорок пять потраченных минут) 
       бодрость = бодрость - 10  & ! можно записать короче бодрость -= 10 
       голод = голод + 5  & ! можно записать короче голод += 5 
       gs 'calendar'  & ! пересчитываем значения 
       gt 'station'  & ! отправляемся на локацию вокзала 
end
Чтобы ограничивать значения статов, нужно всякий раз проверять условие. Например для голода: 

Код:
голод+=5 
if голод > 100: голод = 100
Если тебе лень всякий раз писать это, или ты боишься забыть прописать проверку условия, можно написать универсальную локацию: 
!golod 

Код:
голод += args[0] 
if голод >100: голод = 100 
if голод < 0: голод = 0
Тогда, чтобы изменить значение голода, нужно будет писать: 

Код:
gosub '!golod',5 & ! голод увеличился на 5 единиц 
gosub '!golod',-15 & ! голод уменьшился на 15 единиц
P.S. И ещё, просьба, помещай код в блок для кода, кнопка http://qsp.su/components/com_agora/img/agoraBBCode/code.png
--------------------------------------------------------------
=CH_B_L=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=173&prc=25&Itemid=57#p23825
Здравствуйте! 
(Я не особо шарю в программировании, так что скорее всего это будет немного глупый вопрос) 
Я хочу сделать так, чтобы в дополнительном окне описания были статы и показатели моего персонажа, которые динамически бы отражали изменение какого-либо из указанных параметров. Пока что я сделал так: создал отдельную локацию "Статы персонажа" с кодом (просто пример): 

Код:
CLR 
P '<<$charname>>' 
PL '<<HP>>/<<HPmax>>'
И теперь для того, чтобы "обновить" окно параметров, мне надо писать 

Код:
gosub 'Статы персонажа'
в конце любого действия с этими переменными, что лично мне кажется как минимум немного признаком быдлокодинга. Можете ли Вы подсказать эффективный способ осуществления этой затеи?
=Nex=
это единственно правильный и надёжный способ. 

Впрочем, код можно немного сократить, если сделать локацию для изменения жизней. Тогда можно будет вместо 

Код:
hp = hp - 10 
GS 'stat' 
написать 

Код:
GS 'hp', -10 
В локации 'hp' изменить переменную и вызвать "stat".
---------------------------------------------------------------------------------------
=Darvin432=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=175&prc=25&Itemid=57#p23922
пасибо. очень помогло. Ещё вопрос:Как задать максимальное значение? То есть что бы здоровье нельзя было вылечить выше максимума? команда здоровье MAX=100 (это всё до чего я додумался) не работает

=evp=
MAXhealth = 100 
!Процедура лечения 
if health < MAXhealth: act "Лечить": health = iif (health + 20 < MAXhealth, health + 20, MAXhealth)
---------------------------------------------------------------------------------------
=Darvin432=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=196&prc=25&Itemid=57#p25161
Помогите нубу) 
Как задать максимальное колличество переменной? к примеру если здоровье=100 то выше этого значения подняться не может

=jeesson=
if Здоровье>100:Здоровье=100

=Oliver=
Это не практично, если игрок может увеличить здоровье. 
Darvin432, 
Лучше уж так: 

Код:
if health>mhealth: health=mhealth
------------------------------------------------------------------------------------
=Genryzz=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=215&prc=25&Itemid=57#p26783
Ок. А как заставить переменные меняться? В том окне они статичны. 
Т.е. 
kol = 5 
pl 'У вас <<kol>> яблок' 
В другой локации 
kol += 1 
В окне: 
"У вас 5 яблок" 
Проверяем kol = 6

=oleksandrigo=
Нужно обновлять информацию в окне. 

Код:

act 'взять яблоко': apple += 1 & gt $curloc 
!или же 
act 'взять яблоко': apple += 1 & clr & pl 'У вас <<apple>> яблок' 
Короче нужно чтобы оно заново прогрузило информацию.

=Genryzz=
Не вариант. Каждый раз писать что именно лежит в доп.окне особенно если переменных более чем одна и в разных локациях разные меняются. 
Просто я видел в кодах как есть переменные в доп. окне меняется БЕЗ указания pl 'У вас <<apple>> яблок' и уж тем более без gt $curloc.(никаких ссылок на локации с pl только изменение переменных и отображение их) 
Насколько я понял работало это через функцию $ONOBJSEL и dynamic. Но как они именно работали я вообще не понимаю. Пробовал создавать $ONOBJSEL но результата 0. Он на существование связанной с локацией никак не реагировал. 
Скорее всего работает следующее: 

Нашел вариант но не слишком удобный. 

Код:

clr 
*clr 
gs'stats' 
В stats данные выводящиеся в доп окно. 
Вариант рабочий но альтернативы есть только как они работают я не понимаю.

=oleksandrigo=
Ну я просто сделал локацию которая отвечает за доп окно. И при каждом курлоке она обновляется. 


Код:

!локация доп окно 
clr 
if $PLAYERTYPE = 'Android': pl '<a href="exec:gs ''Доп_окно''">Обновить</a>' 
gs 'Время' 
gs 'Число-Текст' 

pl 'Время -- <<hour>>:<<minut>> 
Дата -- <<day>> <<$month>> <<Year>> года ' 
nl 
pl 'Здоровье <font color = "red"><<Hp>>/<<HPMax>></font>' 
if provNaSus4estvovanieMani = 1: pl 'Мана <font color = "blue"><<Mana>>/<<ManaMax>></font>' 
pl 'Выносливость <font color = "green"><<Stamina>>/<<StaminaMax>></font>' 

!локация onNewLoc 
! это локация обработчик перехода. Короче при каждом курлоке будет выполняться код на этой локации. 
gs 'Доп_окно' 
Чтобы сделать локацию обработчик перехода нужно в самой первой локации(старт, начало, start...) написать 

Код:

$onNewLoc = 'onNewLoc'
Насчет команды gosub или же gs. Она грузит локацию, но не переходит на нее. Это нужно как раз для таких ситуаций. Да и не только.
---------------------------------------------------------------------------
=новичок=
http://qsp.su/index.php?option=com_agora&task=topic&id=40&p=221&prc=25&Itemid=57#p26965
только не смейтесь, хотя можете и смеяться. но как сделать начальные статы для гг и что бы они при каких-то действиях повышались. в этом вопросе вообще ничего не понимаю.

=Последний из Гаяр=
В первой локации напиши переменные. К примеру:

Код:
сила=5 
восприятие=7 
выносливость=2 
харизма=6 
интеллект=5 
ловкость=8 
удача=3 
и так далее. 
Характеристики указывать можешь любые. А дальше можешь просто менять переменные. Всё

=Engineer=
Ничего смешного в незнании нет. Все с чего-то начинали. Однако вопрос действительно невероятно легкий, и ставит под вопрос твое желание искать информацию и обучаться. 
Нужно читать документацию http://wiki.qsp.su/, гуглить форум и вскрывать простые вскрываемые игры.

=Aleks Versus=
Может помогут вот эти сообщения:
http://qsp.su/index.php?option=com_agora&task=topic&prc=25&p=145&id=40&Itemid=57#p22094
http://qsp.su/index.php?option=com_agora&task=topic&prc=25&p=173&id=40&Itemid=57#p23825